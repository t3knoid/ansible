provider "proxmox" {
  pm_api_url      = var.pve_api_url
  pm_user         = var.pve_user
  pm_password     = var.pve_password
  pm_tls_insecure = var.pve_tls_insecure
}

resource "proxmox_vm_qemu" "vm" {
  name        = var.vm_name
  target_node = var.pve_node
  agent    = var.vm_config.agent
  cpu {
    cores = var.vm_config.cores
    type  = var.vm_config.cpu
    sockets = var.vm_config.sockets
    }
  memory   = var.vm_config.memory
  boot     = var.vm_config.boot_order
  scsihw   = var.vm_config.scsihw
  vm_state = "running"
  automatic_reboot = true
  qemu_os  = var.vm_config.ostype

  # Clone from template discovered by Ansible
  clone      = var.template_name
  full_clone = true

  # Boot disk
  disk {
    slot     = var.vm_config.disk_os.disk
    size     = "${var.vm_config.disk_os.size}G"
    storage  = var.vm_config.disk_os.storage
    type     = "disk"
    backup   = var.vm_config.disk_os.backup
  }

  # Cloud-init disk
  disk {
    slot    = "ide2"
    size    = "4G"
    storage = "local"
    type    = "cloudinit"
    backup  = false
  }

  # Optional second disk (or more)
  dynamic "disk" {
    for_each = var.extra_disks
    content {
      slot    = disk.value.disk
      id      = disk.value.id
      type    = "disk"
      storage = disk.value.storage
      size    = disk.value.size
      backup  = disk.value.backup
      # cache = "none" is default in Proxmox provider
    }
  }

  # Cloud-init
  cicustom = "user=local:snippets/${var.vm_name}-user-data-ci.yml,network=local:snippets/${var.vm_name}-network-data-ci.yml"

  # Serial console
  serial {
    id   = 0
    type = "socket"
  }

  # Network interface
  network {
    id     = 0
    bridge = var.vm_config.nic0.bridge
    model  = var.vm_config.nic0.model
  }

  # Do not start on Proxmox host boot
  start_at_node_boot = false
}
